apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc2"

    defaultConfig {
        applicationId "com.vshkl.calcpoly"
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        ndk {
            moduleName "CPoly"
            cFlags "-std=c++11 -fexceptions -frtti"
            stl "gnustl_shared"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jjoe64:graphview:4.0.1'
    compile 'com.android.support:support-v4:22.2.0'
}

// Location of where to store the jni wrapped files
def coreWrapperDir = new File("${projectDir}/src/main/java/com/vshkl/core")

task createCoreWrapperDir {
    coreWrapperDir.mkdirs()
}

// For this to work, it's assumed SWIG is installed
// TODO: This only works when called from Command Line (gradlew runSwig)
task runSwig(type: Exec, dependsOn: ['createCoreWrapperDir']) {

    String osName = System.getProperty("os.name").toLowerCase();
    if (osName.contains("windows")) {
        workingDir '/src/main/jni'   // This implicitly starts from $(projectDir) evidently
        commandLine 'cmd', '/c', 'swig'
        args '-c++', '-java', '-package', 'com.vshkl.core', '-outdir', coreWrapperDir.absolutePath, 'CPoly.i'
    } else {
        commandLine 'swig'
        args '-c++', '-java', '-package', 'com.vshkl.core', '-outdir', coreWrapperDir.absolutePath, "${projectDir}/src/main/jni/CPoly.i"
    }

}